// =========================================================================
// Plugin Declaration
// =========================================================================

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'org.ajoberstar.grgit' version '2.2.0'
    id 'org.springframework.boot' version '2.0.1.RELEASE'
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'

// =========================================================================
// Project Information
// =========================================================================

description = 'A simple web application illustrating how to structure a SpringMVC project.'

// =========================================================================
// Project Attributes
// =========================================================================

def head = grgit.head().abbreviatedId

group = 'com.github.owenfarrell'
version = grgit.describe(match: ['v*'])?.substring(1)?.replace(head, 'SNAPSHOT') ?: "0.0-${grgit.log().size()}-SNAPSHOT"
status = version.endsWith('SNAPSHOT') == false ? 'release' : grgit.branch.current.name == 'master' ? 'milestone' : 'integration'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// =========================================================================
// Dependency Management
// =========================================================================

ext['logback.version'] = '1.2.+'
ext['jackson.version'] = '2.9.+'
ext['slf4j.version'] = '1.7.+'
ext['spring.version'] = '5.0.+'

repositories {
    mavenCentral()
}

dependencies {
    // OWASP Sanitizer
    implementation group: 'com.googlecode.owasp-java-html-sanitizer', name: 'owasp-java-html-sanitizer', version: '20171016.1'

    // Webjars
    runtimeOnly group: 'org.webjars', name: 'webjars-locator', version: '0.33'
    runtimeOnly group: 'org.webjars', name: 'bootstrap', version: '4.0.0-2'
    runtimeOnly group: 'org.webjars', name: 'font-awesome', version: '4.7.0'
    runtimeOnly group: 'org.webjars', name: 'html5shiv', version: '3.7.3-1'
    runtimeOnly group: 'org.webjars', name: 'jquery', version: '3.3.1-1'
    runtimeOnly group: 'org.webjars', name: 'popper.js', version: '1.12.9-1'
    runtimeOnly group: 'org.webjars', name: 'respond', version: '1.4.2-1'

    // Spring Boot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    // Apache Tomcat
    providedRuntime group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper'
    providedRuntime group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'

    // Java Servlet
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0', force: true
    compileOnly group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version: '2.3.1', force: true
    implementation group: 'javax.servlet', name: 'jstl', version: '1.2'
}

// =========================================================================
// Task Type Configuration
// =========================================================================

tasks.withType(JacocoReport) {
    reports.xml.enabled true
}
check.dependsOn tasks.withType(JacocoReport)

tasks.withType(Jar) {
    version = null
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

tasks.withType(Test) {
    jacoco.append = false
    systemProperties['java.io.tmpdir'] = temporaryDir.absolutePath
}

// =========================================================================
// Plugin Task Configuration
// =========================================================================

jacoco {
    toolVersion = '0.8.+'
}

processResources {
    expand(project.properties)
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '4.7'
}
